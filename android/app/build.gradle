import java.io.FileInputStream
import java.util.Properties
import java.util.zip.ZipOutputStream
import java.io.FileOutputStream
import org.gradle.api.tasks.bundling.Zip

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id 'dev.flutter.flutter-gradle-plugin'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    println("WARNING: Release keystore not configured. Copy android/key.properties.template to android/key.properties before publishing.")
}

android {
    namespace = "com.kaushik.sensorysafari"
    compileSdk = 35
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "com.kaushik.sensorysafari"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion = flutter.minSdkVersion
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel = 'FULL'
            }
        }
    }
}

flutter {
    source = "../.."
}

def nativeSymbolsOutputDir = file("${rootProject.buildDir}/app/outputs/native-debug-symbols/release")

tasks.register("zipReleaseNativeSymbols", Zip) {
    group = "distribution"
    description = "Package native libraries from the release build for Play Console symbol uploads."
    dependsOn("mergeReleaseNativeLibs")
    archiveFileName = "native-debug-symbols.zip"
    destinationDirectory.set(nativeSymbolsOutputDir)
    from("${rootProject.buildDir}/app/intermediates/merged_native_libs/release/out/lib") {
        into("symbols")
    }
}

afterEvaluate {
    tasks.named("bundleRelease").configure {
        finalizedBy("zipReleaseNativeSymbols")
    }

    tasks.named("mergeReleaseJavaResource").configure {
        outputs.upToDateWhen { false }
        doLast {
            def mergedJar = file("${rootProject.buildDir}/app/intermediates/merged_java_res/release/base.jar")
            if (!mergedJar.exists()) {
                mergedJar.parentFile.mkdirs()
                new ZipOutputStream(new FileOutputStream(mergedJar)).close()
            }
        }
    }
}
